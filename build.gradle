repositories {
    mavenCentral()
    google()
}

apply plugin: "java-library"
apply plugin: "eclipse"
apply plugin: "idea"

// Simple architecture detection for SWT
def swtArch = System.getenv('CRAFT_ARCH_BUILD_FOR') ?: System.getProperty('os.arch')
def swtPlatform = [
        'amd64': 'gtk.linux.x86_64',
        'x86_64': 'gtk.linux.x86_64',
        'arm64': 'gtk.linux.aarch64',
        'aarch64': 'gtk.linux.aarch64',
        'armhf': 'gtk.linux.arm',
        'arm': 'gtk.linux.arm'
][swtArch] ?: 'gtk.linux.x86_64'

logger.info("Building for architecture: ${swtArch} -> SWT platform: ${swtPlatform}")

configurations.configureEach {
    resolutionStrategy {
        eachDependency {
            if (requested.name.contains('${osgi.platform}')) {
                useTarget requested.toString().replace('${osgi.platform}', swtPlatform)
            }
        }
    }
}

dependencies {
    implementation 'org.eclipse.platform:org.eclipse.swt:3.130.0'
    implementation 'org.eclipse.platform:org.eclipse.core.jobs:3.15.500'
}

compileJava {
    options.release = 21
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}


project.ext.appName = "PokeMMO"
project.ext.mainClassName = "com.pokeemu.unix.UnixInstaller"
project.ext.mainJar = "unix-installer.jar"

// creates a slim JDK runtime for distribution
task createRuntime(type: Exec) {
    doFirst() {
        project.delete("${buildDir}/runtime")
    }

    String runtimePath = "${buildDir}/runtime"
    String jlinkPath = "$System.env.JRE_TARGET" + "/bin/jlink"

    workingDir project.projectDir
    commandLine = [
            jlinkPath,
            '--add-modules', 'java.se,java.base,java.desktop,jdk.unsupported,jdk.crypto.ec',
            '--strip-debug',
            '--no-header-files',
            '--no-man-pages',
            "--vm=server",
            "--compress=2",
            '--output', runtimePath
    ]
}

jar {
    archiveFileName = project.mainJar

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                'Main-Class': project.mainClassName
        )
    }
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

task dist(type: GradleBuild) {
    buildName "dist"
    startParameter.projectProperties = gradle.startParameter.projectProperties
    tasks = ['clean', 'createRuntime', 'jar']
}

assemble.dependsOn dist